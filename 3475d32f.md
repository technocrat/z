---
date: 2021-05-08T14:03
---

# Latin squares

[[[Algorithms]]]
[[Haskell]]

[General solution](https://stackoverflow.com/questions/59918998/matrix-of-string-with-unique-columns-and-rows-latin-square)

    {-# OPTIONS_GHC -Wall #-}

    module LatinAll where

    import Control.Monad.State
    import Data.List
    import Data.Set (Set)
    import qualified Data.Set as Set
    import Data.Map (Map, (!))
    import qualified Data.Map as Map

    data S = S
      { size :: Int
      , rs :: Map Int [Int]
      , cs :: Set (Int, Int) }

    data Square = Square
      Int   -- square size
      [Int] -- symbols in row-major order
      deriving (Show)

    type M = StateT S []

    -- Get Latin squares
    latin :: M Square
    latin = do
      n <- gets size
      cells <- forM (pairs n) (\(r,c) -> getS r c)
      return $ Square n cells

    -- All locations in row-major order [(1,1),(1,2)..(n,n)]
    pairs :: Int -> [(Int,Int)]
    pairs n = (,) <$> [1..n] <*> [1..n]

    -- Get a valid `s` for position `(r,c)`.
    getS :: Int -> Int -> M Int
    getS r c = do
      s <- pickSFromRow r
      pickCS c s
      return s

    -- Get an available `s` in row `r` from the `rs` urn.
    pickSFromRow :: Int -> M Int
    pickSFromRow r = do
      urn <- gets rs
      (s, rest) <- lift $ choices (urn ! r)
      modify (\st -> st { rs = Map.insert r rest urn })
      return s

    -- Remove `(c,s)` from the `cs` urn.
    pickCS :: Int -> Int -> M ()
    pickCS c s = do
      balls <- gets cs
      guard $ (c,s) `Set.member` balls
      modify (\st -> st { cs = Set.delete (c,s) balls })

    -- Return all ways of removing one element from list.
    choices :: [a] -> [(a,[a])]
    choices = init . (zipWith f <$> inits <*> tails)
      where f a (x:b) = (x, a++b)
            f _ _ = error "choices: internal error"

    -- Run an action in the M monad.
    runM :: Int -> M a -> [a]
    runM n act = evalStateT act (S n rs0 cs0)
      where rs0 = Map.fromAscList $ zip [1..n] (repeat [1..n])
            cs0 = Set.fromAscList $ pairs n

    main :: IO ()
    main = do
      print $ runM 3 latin
      print $ length (runM 4 latin)
      print $ length (runM 5 latin)
      
For reduced Latin Square (given top column and left rows fixed)

    -- All locations in row-major order, skipping first row and column
    -- i.e., [(2,2),(2,3)..(n,n)]
    pairs :: Int -> [(Int,Int)]
    pairs n = (,) <$> [2..n] <*> [2..n]

    -- Run an action in the M monad.
    runM :: Int -> M a -> [a]
    runM n act = evalStateT act (S n rs0 cs0)
      where -- skip balls [(1,1)..(n,n)] for first row
            rs0 = Map.fromAscList $ map (\r -> (r, skip r)) [2..n]
            -- skip balls [(1,1)..(n,n)] for first column
            cs0 = Set.fromAscList $ [(c,s) | c <- [2..n], s <- skip c]
            skip i = [1..(i-1)]++[(i+1)..n]